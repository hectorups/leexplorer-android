apply plugin: 'android'
apply plugin: 'android-apt'
apply plugin: 'crashlytics'
apply plugin: 'pmd'
apply plugin: 'findbugs'

repositories {
  maven { url 'http://download.crashlytics.com/maven' }
}

def getAndroidAppVersionCode() {
  return 'git rev-list HEAD --count'.execute().text.trim().toInteger()
}

android {
  compileSdkVersion 19
  buildToolsVersion "19.1.0"

  defaultConfig {
    applicationId "com.leexplorer.app"
    minSdkVersion 10
    targetSdkVersion 19
    versionCode getAndroidAppVersionCode()
    versionName "1.0"

    buildConfigField "boolean", "FAKE_DATA", "false"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  signingConfigs {
    release {
      storeFile file(LE_STORE_FILE)
      storePassword LE_STORE_PASSWORD
      keyAlias LE_KEY_ALIAS
      keyPassword LE_KEY_PASSWORD
    }
  }

  buildTypes {
    debug {
      runProguard false
      applicationIdSuffix '.debug'
    }
    release {
      runProguard false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
      signingConfig signingConfigs.release
    }
  }

  lintOptions {
    abortOnError false
  }
}

dependencies {
  compile 'com.android.support:appcompat-v7:19.+'
  compile 'com.squareup.picasso:picasso:2.3.2'
  compile 'com.nineoldandroids:library:2.4.0'
  compile 'com.netflix.rxjava:rxjava-core:0.19.6'
  compile 'com.netflix.rxjava:rxjava-android:0.19.6'
  compile 'com.jakewharton:butterknife:5.1.1'
  compile 'com.squareup.retrofit:retrofit:1.6.1'
  compile 'com.google.code.gson:gson:2.2.4'
  compile 'com.etsy.android.grid:library:1.0.4'
  compile 'com.google.android.gms:play-services:4.4.52'
  compile project(':paralloidviews')
  compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
  compile 'com.crashlytics.android:crashlytics:1.+'
  compile 'com.squareup.dagger:dagger:1.2.1'
  apt 'com.squareup.dagger:dagger-compiler:1.2.1'
  compile 'com.squareup.okhttp:okhttp:2.0.0'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
  compile 'com.squareup:pollexor:2.0.2'
  compile 'com.daimajia.numberprogressbar:library:1.1@aar'
  compile 'com.squareup:otto:1.3.4'
  compile 'de.keyboardsurfer.android.widget:crouton:1.8.4'
  compile 'com.mixpanel.android:mixpanel-android:4.2.2@aar'
}

pmd {
  toolVersion = '5.1.0'
}

findbugs {
  toolVersion = "2.0.3"
}


task findbugs(type: FindBugs) {
  excludeFilter file('config/findbugs/exclude.xml')
  classes = fileTree('build/intermediates/classes/release')
  source = fileTree('src/main/java')
  classpath = files()
  effort = 'max'
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

task pmd(type: Pmd) {
  ruleSets = ["java-android",
              "java-basic",
              "java-braces",
              "java-strings",
              "java-unnecessary",
              "java-unusedcode",
              "java-sunsecure"]
  source = fileTree('src/main/java')
}

//task findbugsWrapper(dependsOn: 'findbugs') {}
task pmdWrapper(dependsOn: 'pmd') {}

//check.dependsOn findbugsWrapper
check.dependsOn pmdWrapper